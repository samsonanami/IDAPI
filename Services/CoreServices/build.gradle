ext {
    javaLanguageLevel = '1.8'
    generatedMapperSourcesDir = "${buildDir}/generated-src"
}

configurations {
    mapstruct
}

sourceSets.main {
    ext.originalJavaSrcDirs = java.srcDirs
    java.srcDir "${generatedMapperSourcesDir}"
}

dependencies {
    compile group: 'org.springframework', name: 'spring-jdbc', version: '4.3.2.RELEASE'
    compile group: 'org.springframework', name: 'spring-test', version: '4.3.2.RELEASE'
    compile group: 'org.mapstruct', name: 'mapstruct-jdk8', version: '1.0.0.Final'
    mapstruct( 'org.mapstruct:mapstruct-processor:1.0.0.Final' )

    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
    testCompile group: 'junit', name: 'junit', version: '4.11'


    compile project (':OrionDataAbstraction')
    compile project (':DataTransferObjects')
    compile project (':JobMessagingChannelHandler')
}

task generateMainMapperClasses(type: JavaCompile) {
    ext.aptDumpDir = file( "${buildDir}/tmp/apt/mapstruct" )
    destinationDir = aptDumpDir

    classpath = compileJava.classpath + configurations.mapstruct
    source = sourceSets.main.originalJavaSrcDirs
    ext.sourceDestDir = file ( "$generatedMapperSourcesDir" )

    options.define(
            compilerArgs: [
                    "-nowarn",
                    "-proc:only",
                    "-encoding", "UTF-8",
                    "-processor", "org.mapstruct.ap.MappingProcessor",
                    "-s", sourceDestDir.absolutePath,
                    "-source", javaLanguageLevel,
                    "-target", javaLanguageLevel,
            ]
    );

    inputs.dir source
    outputs.dir generatedMapperSourcesDir;
    doFirst {
        sourceDestDir.mkdirs()
    }
    doLast {
        aptDumpDir.delete()
    }
}

compileJava.dependsOn generateMainMapperClasses